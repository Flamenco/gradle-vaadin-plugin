apply plugin: 'groovy'

repositories{
	mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()
    groovy localGroovy()
    deployerJars "org.apache.maven.wagon:wagon-ftp:1.0-beta-2"
}

apply plugin: 'maven'

group = "fi.jasoft.plugin"
version = "0.3.4"
archivesBaseName = 'VaadinPlugin'

uploadArchives {
    repositories.mavenDeployer {
        def repo = System.getProperty('PLUGIN_MAVEN_REPOSITORY', '<plugin repository not defined>') 
        def user = System.getProperty('PLUGIN_MAVEN_REPOSITORY_USER', '<user not defined>')
        def password = System.getProperty('PLUGIN_MAVEN_REPOSITORY_PASSWORD', '<password not defined>')

        name = 'sshDeployer' 
        configuration = configurations.deployerJars
        repository(url: repo) {
            authentication(userName: user, password: password)
        }
	    pom.project {
	            licenses {
	                license {
	                    name 'The Apache Software License, Version 2.0'
	                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	                    distribution 'repo'
	                }
	            }
	    }

	    pom.scopeMappings.mappings.remove(configurations.compile)
    }
}

// Use wrapper
task wrapper(type: Wrapper) { gradleVersion = '1.3' }

// Create pom.xml for Vaadin Directory
task createPOMforVaadinDirectory {

    File pomDir = new File("src/main/resources/META-INF/maven/${project.group}/${project.archivesBaseName}")
    pomDir.mkdirs()

    File pomFile = new File(pomDir.canonicalPath+'/pom.xml')
    outputs.file pomFile

    pom {
        artifactId = project.archivesBaseName
        project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            repositories {
                repository {
                    name 'Jasoft.fi Repository'
                    url 'http://mvn.jasoft.fi/maven2'
                }
            }
            
        }
    }.writeTo(pomFile.canonicalPath)
}

processResources {
    dependsOn project.createPOMforVaadinDirectory
}

jar {
    manifest{
        attributes(
            'Vaadin-Package-Version': 1,
            'Vaadin-License-Title': 'Apache 2.0',
            'Implementation-Title': 'Vaadin Plugin for Gradle',
            'Implementation-Version': version,
            'Implementation-Vendor': 'John Ahlroos',
        )
    }
}